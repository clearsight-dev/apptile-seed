import groovy.json.JsonSlurper

apply plugin: "com.android.application"
apply plugin: "com.google.gms.google-services"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"

// FireworkDependency (Don't remove) apply from: 'firework.gradle'

react {
    autolinkLibrariesWithApp()
}

def enableProguardInReleaseBuilds = false
def jscFlavor = 'io.github.react-native-community:jsc-android:2026004.+'

def configFile = file("../../apptile.config.json")
def appBundleId = "com.apptile.apptilepreviewdemo"
def appVersionCode = 1
def appVersionName = "0.1.0"

def enable_onesignal = false
def enable_moengage = false
def enable_clevertap = false

if (configFile.exists()) {
    def json = new JsonSlurper().parseText(configFile.text)
    if (json.android && json.android.bundle_id) {
        appBundleId = json.android.bundle_id
    }

    if(json.android && json.android.version_code){
        appVersionCode = json.android.version_code
    }

    if(json.android && json.android.version_name){
        appVersionName = json.android.version_name
    }

    if (json.feature_flags.ENABLE_ONESIGNAL) {
      enable_onesignal = json.feature_flags.ENABLE_ONESIGNAL
    }

    if (json.feature_flags.ENABLE_MOENGAGE) {
      enable_moengage = json.feature_flags.ENABLE_MOENGAGE
    }

    if (json.feature_flags.ENABLE_CLEVERTAP) {
      enable_clevertap = json.feature_flags.ENABLE_CLEVERTAP
    }
}

android {
    ndkVersion rootProject.ext.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion

    namespace "com.apptileseed"
    defaultConfig {
        applicationId "com.apptileseed"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        missingDimensionStrategy("react-native-camera", "general")
        
        // Add NDK configuration
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }
    
    // Add packaging options for 16 KB compatibility
    packagingOptions {
        jniLibs {
            useLegacyPackaging false
        }
    }
    
    signingConfigs {
        debug {
            String appUploadStoreFile =  System.getenv("SIGNING_STORE_FILE") ?: 'debug.keystore'
            if (appUploadStoreFile) {
                storeFile file(appUploadStoreFile)
                storePassword System.getenv("SIGNING_STORE_PASSWORD") ?: 'android'
                keyAlias System.getenv("SIGNING_KEY_ALIAS") ?: 'androiddebugkey'
                keyPassword System.getenv("SIGNING_KEY_PASSWORD") ?: 'android'
            }
        }
        release {
            def storeFilePath     = project.findProperty("MYAPP_UPLOAD_STORE_FILE") ?: "default-release.keystore"
            def storePass         = project.findProperty("MYAPP_UPLOAD_STORE_PASSWORD") ?: "defaultStorePass"
            def alias             = project.findProperty("MYAPP_UPLOAD_KEY_ALIAS") ?: "defaultAlias"
            def keyPass           = project.findProperty("MYAPP_UPLOAD_KEY_PASSWORD") ?: "defaultKeyPass"

            println "Release signing store file: ${storeFilePath}"
            println "Release signing store password: ${storePass}"
            println "Release signing key alias: ${alias}"
            println "Release signing key password: ${keyPass}"

            storeFile file(storeFilePath)
            storePassword storePass
            keyAlias alias
            keyPassword keyPass
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.debug
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    aaptOptions {
        noCompress "txt", "json"
    }
    buildFeatures {
        prefab true
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging false
        }
        pickFirst '**/libc++_shared.so'
        pickFirst '**/libfbjni.so'
        pickFirst '**/libreact_featureflagsjni.so'
        pickFirst '**/libreact_nativemodule_core.so'
    }
    externalNativeBuild {
        cmake {
            version "3.22.1"
        }
    }
    sourceSets {
      main {
        assets {
          srcDirs "src/main/assets"
        }

        if (enable_moengage) {
          java.srcDir 'src/moengage/enabled/java'
        } else {
          java.srcDir 'src/moengage/disabled/java'
        }

        if (enable_clevertap) {
          java.srcDir 'src/clevertap/enabled/java'
        } else {
          java.srcDir 'src/clevertap/disabled/java'
        }
      }
    }
}

dependencies {
    implementation("com.facebook.react:react-android")

    // Add these dependencies for new architecture support
    implementation 'com.facebook.fbjni:fbjni:0.2.2'
    implementation("com.facebook.fresco:fresco:3.6.0")
    implementation("com.facebook.fresco:animated-gif:3.6.0")
    
    implementation("androidx.core:core:1.16.0")
    implementation("androidx.appcompat:appcompat:1.7.1")
    implementation("androidx.lifecycle:lifecycle-process:2.9.2")
    implementation("androidx.core:core-splashscreen:1.0.1")
    implementation("androidx.work:work-runtime-ktx:2.10.3")
    
    implementation(platform("com.google.firebase:firebase-bom:34.1.0"))
    implementation("com.google.firebase:firebase-messaging-ktx:24.1.2")
    implementation("com.google.guava:guava:33.4.8-android")
    implementation("com.google.android.play:app-update:2.1.0")
    
    implementation 'io.sentry:sentry-android:7.0.0'

    // in app reviews
    implementation 'com.google.android.play:review:2.0.2'
    implementation 'com.google.android.play:review-ktx:2.0.2'

    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }

    if (enable_onesignal) {
      implementation 'com.onesignal:OneSignal:[5.0.0, 5.99.99]'
    }

    if (enable_clevertap) {
      implementation 'com.clevertap.android:clevertap-android-sdk:5.0.0'
      implementation 'com.android.installreferrer:installreferrer:2.2'
    }

    if(enable_moengage) {
        implementation(moengage.core)
        implementation(moengage.richNotification)
        implementation(moengage.inapp)
    }
}

apply from: "./apptile.gradle"

def exportPath = System.getenv("exportPath")

android.applicationVariants.all { variant ->
    if (variant.buildType.name == "release") {
        variant.outputs.all { output ->
            // Assemble Release task
            tasks.named("assemble${variant.name.capitalize()}").configure { task ->
                doLast {
                    if (exportPath) {
                        // Create export directory if it doesn't exist
                        def exportDir = new File(exportPath)
                        if (!exportDir.exists()) {
                            exportDir.mkdirs()
                        }
                        
                        // Copy APK to export path
                        def apkFile = new File(output.outputFile.absolutePath)
                        if (apkFile.exists()) {
                            copy {
                                from apkFile
                                into exportDir
                            }
                            println "Exported APK to ${exportDir.absolutePath}/${apkFile.name}"
                        }
                    }
                }
            }
            
            // Bundle Release task
            tasks.matching { it.name == "bundle${variant.name.capitalize()}" }.all { task ->
                doLast {
                    if (exportPath) {
                        // Create export directory if it doesn't exist
                        def exportDir = new File(exportPath)
                        if (!exportDir.exists()) {
                            exportDir.mkdirs()
                        }
                        
                        // Copy AAB to export path
                        def aabDir = new File("${project.buildDir}/outputs/bundle/${variant.name}")
                        if (aabDir.exists()) {
                            aabDir.listFiles().findAll { it.name.endsWith(".aab") }.each { aabFile ->
                                copy {
                                    from aabFile
                                    into exportDir
                                }
                                println "Exported AAB to ${exportDir.absolutePath}/${aabFile.name}"
                            }
                        }
                    }
                }
            }
        }
    }
}